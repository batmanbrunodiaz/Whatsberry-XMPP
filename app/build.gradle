apply plugin: 'com.android.application'

// Here you can set a link to a zip file containing your app sources.
// If you want your app to be available offline. (SOURCE MUST BE A ZIP FILE WITH ALL FILES IN ROOT)
tasks.register('downloadWebSources') {
    def src = ''
    doLast {
        if (!src || src.trim().isEmpty()) {
            println "Source URL is empty, skipping download..."
            return
        }
        
        def destdir = '.'
        def destfile = "$destdir/package.zip"
        def f = new File(destfile)
        f.getParentFile()?.mkdirs()
        if (f.exists()) {
            f.delete()
        }
        println "Downloading $destfile from $src..."
        def url = new URL(src)
        url.withInputStream { i -> f.withOutputStream { it << i } };
        copy {
            def outputDir = "./src/main/assets"
            def packagePath = "../package.zip"
            delete(outputDir)
            mkdir(outputDir)
            from zipTree(packagePath)
            into outputDir
        }
        f.delete()
    }
}

android {
    namespace 'com.whatsberry.xmpp'
    //noinspection GradleDependency
    compileSdkVersion 18

    tasks.withType(JavaCompile).configureEach {
        compileTask -> compileTask.dependsOn downloadWebSources
    }

    defaultConfig {
        applicationId "com.whatsberry.xmpp"
        minSdkVersion 18
        multiDexEnabled false
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 18
        versionCode 331
        versionName "3.3.1"

        // XMPP credentials now configured by user (not hardcoded)
        buildConfigField "String", "XMPP_SERVER", "\"\""
        buildConfigField "String", "XMPP_USERNAME", "\"\""
        buildConfigField "String", "XMPP_PASSWORD", "\"\""
        buildConfigField "int", "XMPP_PORT", "5222"
    }

    signingConfigs {
        debug {
            // Configuraci√≥n para API 18 (BlackBerry 10)
            v1SigningEnabled true
            v2SigningEnabled false
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a", "x86"
            }
        }
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "armeabi-v7a"
            }
        }
    }

    productFlavors {
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    buildFeatures {
        buildConfig true
    }
}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':crosswalk')

    // Smack XMPP library (compatible with Android API 16+)
    implementation 'org.igniterealtime.smack:smack-android:4.1.9'
    implementation 'org.igniterealtime.smack:smack-tcp:4.1.9'
    implementation 'org.igniterealtime.smack:smack-im:4.1.9'
    implementation 'org.igniterealtime.smack:smack-extensions:4.1.9'
    implementation 'org.igniterealtime.smack:smack-experimental:4.1.9'

    // For JSON handling (QR codes, etc)
    implementation 'com.google.code.gson:gson:2.8.5'

    // ZXing for QR code generation (core only for size)
    implementation 'com.google.zxing:core:3.3.0'
}